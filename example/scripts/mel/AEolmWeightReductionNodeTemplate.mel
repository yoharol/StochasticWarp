global proc AEolmWeightReductionNodeTemplate( string $nodeName )
{
	editorTemplate -beginScrollLayout;
	editorTemplate -beginLayout "Reduction Parameters" -collapse false;
	
	editorTemplate -callCustom "ReductionRatioNew"
							   "ReductionRatioReplace"
							   "reductionRatio";
							   
	editorTemplate -callCustom "MaximumWeightLengthNew"
							   "MaximumWeightLengthReplace"
							   "maxWeightLen";
	   							   
	editorTemplate -callCustom "ReductionTypeNew"
							   "ReductionTypeReplace"
							   "reducerType";
							   
	editorTemplate -callCustom "RegularNew"
								"RegularReplace"
								"regularization";
								
	editorTemplate -endLayout;
	editorTemplate -addExtraControls;
	editorTemplate -endScrollLayout;

	// hide some unnecessary attributes
	editorTemplate -suppress "input";
	editorTemplate -suppress "caching";
	editorTemplate -suppress "nodeState";
	editorTemplate -suppress "mixedInput";	
	editorTemplate -suppress "map64BitIndices";
	editorTemplate -suppress "reducedInput";
	editorTemplate -suppress "cages";
	editorTemplate -suppress "poses";
}

// reductionRatio
global proc ReductionRatioNew(string $attrName)
{
	floatSliderGrp -label "Ratio" -field true -minValue 92.0 -maxValue 99.9 
					-fieldMinValue 92.0 -fieldMaxValue 99.9 -step 0.1 reductionRatioSliderGrp;
	connectControl reductionRatioSliderGrp $attrName;
}
global proc ReductionRatioReplace( string $attrName )
{
   connectControl reductionRatioSliderGrp $attrName;
}


// maxWeightLen
global proc MaximumWeightLengthNew(string $attrName)
{
	intSliderGrp -label "Maximum weights" -field true -minValue 8 -maxValue 30 weightAmountSliderGrp;
	connectControl weightAmountSliderGrp $attrName;
}
global proc MaximumWeightLengthReplace( string $attrName )
{
   connectControl weightAmountSliderGrp $attrName;
}


// reducerType
global proc ReductionTypeNew(string $attrName)
{
	optionMenu -label "Type" reductionTypeMenu;
		menuItem -label "Largest" -data 1;
		menuItem -label "Geometry" -data 2;
		menuItem -label "Poisson" -data 3;
		menuItem -label "Improved Poisson" -data 4;
	connectControl reductionTypeMenu $attrName;
}
global proc ReductionTypeReplace( string $attrName )
{
   connectControl reductionTypeMenu $attrName;
}

global proc RegularNew(string $attrName)
{
	checkBox -label "Regularization" regularCheckButton;
	connectControl regularCheckButton $attrName;
}
global proc RegularReplace( string $attrName )
{
   connectControl regularCheckButton $attrName;
}